buildscript {
    repositories {
        mavenLocal()
        maven {url 'https://jenkins.cs.ox.ac.uk/artifactory/plugins-snapshot'}
        maven {url 'https://jenkins.cs.ox.ac.uk/artifactory/plugins-release'}
        mavenCentral()
    }
    dependencies {
        classpath "uk.ac.ox.softeng.maurodatamapper.gradle:mdm-gradle-plugin:$mdmGradlePluginVersion"
    }
}

plugins {
    id 'groovy'
    id 'java-library'
    id 'antlr'
}

apply plugin: 'uk.ac.ox.softeng.maurodatamapper.gradle.mdm-gradle'

// Define new "generation" sourceset for generating the dita library
sourceSets {
    // Source is the generation code and the antlr generated source
    generation {
        groovy.srcDirs = ['src/generation/groovy', "${project.buildDir}/generated-src/generation/antlr"]
    }
    // Add the dita generated source into the main src
    main {
        groovy.srcDirs += ["${project.buildDir}/generated-src/main/dita"]
    }
}

// Setup the generation sourceset configurations to extend from the existing configurations
// Note that the antlr plugin adds a new configuration which is loaded into the compileClasspath
configurations {
    generationImplementation.extendsFrom implementation
    generationRuntimeOnly.extendsFrom runtimeOnly
}

test {
    useJUnitPlatform()
}

jar{
    exclude 'logback.*', 'log4j.*'
}

// Make sure we dont stamp addtl files with our license
license {
    excludes([
        '**/dita-ot-*/*',
        '**/*.g4'
    ])
}

// Customise the antlr grammer source to output to the "generation/antlr" directory
// Standard format of src dirs is "sourcesetname/codetype"
generateGrammarSource {
    outputDirectory = project.file("${project.buildDir}/generated-src/generation/antlr")
    maxHeapSize = "128m"
    arguments += ['-visitor', '-no-listener']
}

// Register a new task to generate the dita source into "main/dita" directory
tasks.register('generateDitaSource', JavaExec) {
    ext.destDir = project.file('build/generated-src/main/dita')
    outputs.dir destDir

    classpath = sourceSets.generation.runtimeClasspath
    mainClass = 'uk.ac.ox.softeng.maurodatamapper.dita.generation.DocumentationParser'
    // arguments to pass to the application
    argsString = 'build/generated-src/main/dita'
}

// Ensure tasks are run in the correct order
// antlr grammer -> generation source set compiled -> dita source generated -> main code compiled
tasks.generateDitaSource.dependsOn 'generationClasses'
tasks.compileGenerationJava.dependsOn 'generateGrammarSource'
tasks.compileJava.dependsOn 'generateDitaSource'

afterEvaluate {
    // Make sure intellij shows the build folder so we can see the generated code
    // And add the src/main/antlr directory (source dir for antlr plugin) to the source listing so it looks right
    idea.module{
        File buildDir = excludeDirs.find{it.name == project.buildDir.name}
        excludeDirs.remove(buildDir)
        sourceDirs << project.file('src/main/antlr')
    }
    jar {
        manifest {
            attributes(
                "Class-Path": configurations.runtimeClasspath.collect {it.getName()}.join(' '))
        }
    }

    // Make sure groovy tasks can compile
    tasks.withType(GroovyCompile) {
        configure(groovyOptions.forkOptions) {
            memoryMaximumSize = '1g'
            jvmArgs = ['-Xms512m', '-Xmx1g']
        }
    }
}

apply from: 'dependencies.gradle'